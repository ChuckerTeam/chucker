apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

def siteUrl = 'https://github.com/olivierperez/chucky' // Homepage URL of the library
def gitUrl = 'https://github.com/olivierperez/chucky.git' // Git repository URL
group = GROUP // Maven Group ID for the artifact

afterEvaluate { proj ->

    install {
        repositories.mavenInstaller {
            // This generates POM.xml with proper parameters
            pom {
                project {
                    packaging 'aar'

                    // Add your description here
                    name "$group:${proj.name}"
                    description = POM_DESCRIPTION
                    url siteUrl

                    // Set your license
                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_SCM_CONNECTION
                        }
                    }
                    developers {
                        developer {
                            id 'olivierperez'
                            name 'Olivier Perez'
                            email 'olivier@olivierperez.fr'
                        }
                    }
                    scm {
                        connection gitUrl
                        developerConnection gitUrl
                        url siteUrl
                    }
                }
            }
        }
    }

    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives javadocJar
        archives sourcesJar
    }

// https://github.com/bintray/gradle-bintray-plugin
    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_API_KEY')

        configurations = ['archives']
        pkg {
            repo = POM_REPO_NAME
            // it is the name that appears in bintray when logged
            name = "$POM_REPO_NAME:${proj.name}"
            websiteUrl = POM_URL
            vcsUrl = POM_SCM_CONNECTION
            issueTrackerUrl = POM_ISSUE_TRACKER
            licenses = [POM_LICENCE]
            publish = true
            publicDownloadNumbers = false
            githubRepo = POM_GITHUB_REPO
            githubReleaseNotesFile = "CHANGELOG.md"
        }
    }
}
