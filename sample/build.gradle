apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.squareup.wire'
apply plugin: 'com.apollographql.apollo3'

wire {
    kotlin {}
}

android {
    compileSdk rootProject.compileSdkVersion
    namespace "com.chuckerteam.chucker.sample"

    defaultConfig {
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion
        applicationId "com.chuckerteam.chucker.sample"
        versionName VERSION_NAME
        versionCode VERSION_CODE.toInteger()
    }

    buildFeatures {
        viewBinding true
        buildConfig = false
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    signingConfigs {
        debug {
            keyAlias 'chucker'
            keyPassword 'android'
            storeFile file('debug.keystore')
            storePassword 'android'
        }
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
        disable 'AcceptsUserCertificates'
        // Don't fail build if some dependencies outdated
        disable 'GradleDependency'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
}

apollo {
    service("rickandmortyapi") {
        packageName.set("com.chuckerteam.chucker.sample")
        schemaFile.set(file("src/main/graphql/com/chuckerteam/chucker/sample/schema.json.graphql"))
        srcDir("src/main/graphql")
        excludes.add("**/schema.json.graphql")
        excludes.add("**/schema.json")
    }
}

dependencies {
    debugImplementation project(':library')
    releaseImplementation project(':library-no-op')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"
    implementation "androidx.activity:activity-ktx:$activityVersion"

    implementation "com.google.android.material:material:$materialComponentsVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    implementation "com.apollographql.apollo3:apollo-runtime:$apolloVersion"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
}

apply from: rootProject.file('gradle/kotlin-static-analysis.gradle')
