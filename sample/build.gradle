apply plugin: 'com.android.application'
apply plugin: 'com.squareup.wire'

wire {
    kotlin {}
}

apply from: "$rootDir/gradle/base.gradle"

android {
    defaultConfig {
        applicationId "com.chuckerteam.chucker.sample"
        versionName VERSION_NAME
        versionCode VERSION_CODE.toInteger()
    }

    buildFeatures {
        viewBinding = true
    }

    sourceSets {
        getByName("main").java.srcDirs += "$buildDir/generated/source/wire/"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    signingConfigs {
        debug {
            keyAlias 'chucker'
            keyPassword 'android'
            storeFile file('debug.keystore')
            storePassword 'android'
        }
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
        disable 'AcceptsUserCertificates'
        // Don't fail build if some dependencies outdated
        disable 'GradleDependency'
    }
}

dependencies {
    debugImplementation project(':library')
    releaseImplementation project(':library-no-op')

    implementation "com.google.android.material:material:$materialComponentsVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
}

apply from: rootProject.file('gradle/kotlin-static-analysis.gradle')
