buildscript {
    ext {
        kotlinVersion = '1.9.22'
        androidGradleVersion = '8.2.1'
        coroutineVersion = '1.7.3'

        // Google libraries
        activityVersion = '1.8.1'
        appCompatVersion = '1.6.1'
        constraintLayoutVersion = '2.1.4'
        materialComponentsVersion = '1.11.0'
        fragmentVersion = '1.6.2'
        roomVersion = '2.6.1'
        lifecycleVersion = '2.7.0'
        androidXCoreVersion = '2.2.0'
        paletteKtxVersion = '1.0.0'
        kspVersion = '1.9.22-1.0.17'

        // Networking
        brotliVersion = '0.1.2'
        gsonVersion = '2.10.1'
        okhttpVersion = '4.11.0'
        retrofitVersion = '2.9.0'
        wireVersion = '4.9.3'

        // Debug and quality control
        binaryCompatibilityValidator = '0.13.2'
        detektVersion = '1.23.4'
        ktLintGradleVersion = '12.1.0'
        leakcanaryVersion = '2.13'

        // Apollo
        apolloVersion = '3.8.2'

        // Testing
        androidxTestCoreVersion = '1.5.0'
        junitGradlePluignVersion = '1.10.0.0'
        junitVersion = '5.10.1'
        junit4Version = '4.13.2'
        mockkVersion = '1.13.9'
        robolectricVersion = '4.11'
        truthVersion = '1.2.0'
        androidXTestRunner = '1.5.2'
        androidXTestRules = '1.5.0'
        androidXTestExt = '1.1.5'

        // Publishing
        nexusStagingPlugin = '0.30.0'
    }

    repositories {
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradleVersion"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:$junitGradlePluignVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.apollographql.apollo3:apollo-gradle-plugin:$apolloVersion"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktLintGradleVersion"
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:$binaryCompatibilityValidator"
        classpath "com.squareup.wire:wire-gradle-plugin:$wireVersion"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:$nexusStagingPlugin"
        classpath "com.google.devtools.ksp:symbol-processing-gradle-plugin:$kspVersion"
    }
}
apply plugin: 'binary-compatibility-validator'
apply plugin: 'io.codearte.nexus-staging'

apiValidation {
    ignoredProjects += ["sample"]
    ignoredPackages += [
            "com.chuckerteam.chucker.internal",
            "com.chuckerteam.chucker.databinding"
    ]
}

allprojects {
    version = VERSION_NAME
    group = GROUP

    repositories {
        google()
        mavenCentral()
    }

    tasks.withType(Test) {
        testLogging {
            events "skipped", "failed", "passed"
        }
    }
}

task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}

task clean(type: Delete) {
    dependsOn(installGitHook)
    delete rootProject.buildDir
}

ext {
    minSdkVersion = 21
    targetSdkVersion = 34
    compileSdkVersion = 34
}

nexusStaging {
    username = findProperty("NEXUS_USERNAME")
    password = findProperty("NEXUS_PASSWORD")
    stagingProfileId = "ea09119de9f4"
}
