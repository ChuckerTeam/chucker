buildscript {
    ext {
        kotlinVersion = '1.5.31'
        androidGradleVersion = '7.0.2'
        coroutineVersion = '1.5.2'

        // Google libraries
        activityVersion = '1.3.1'
        appCompatVersion = '1.3.1'
        constraintLayoutVersion = '2.1.0'
        materialComponentsVersion = '1.4.0'
        roomVersion = '2.3.0'
        fragmentVersion = '1.3.6'
        lifecycleVersion = '2.3.1'
        androidXCoreVersion = '2.1.0'
        paletteKtxVersion = '1.0.0'

        // Networking
        brotliVersion = '0.1.2'
        gsonVersion = '2.8.8'
        okhttpVersion = '4.9.2'
        retrofitVersion = '2.9.0'
        wireVersion = '3.7.0'

        // Debug and quality control
        binaryCompatibilityValidator = '0.7.1'
        detektVersion = '1.18.1'
        dokkaVersion = '1.5.30'
        ktLintGradleVersion = '10.0.0'
        leakcanaryVersion = '2.7'

        // Testing
        androidxTestCoreVersion = '1.4.0'
        junitGradlePluignVersion = '1.8.0.0'
        junitVersion = '5.8.1'
        junit4Version = '4.13.2'
        mockkVersion = '1.12.0'
        robolectricVersion = '4.6.1'
        truthVersion = '1.1.3'

        // Publishing
        nexusStagingPlugin = '0.30.0'
    }

    repositories {
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradleVersion"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:$junitGradlePluignVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktLintGradleVersion"
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:$binaryCompatibilityValidator"
        classpath "com.squareup.wire:wire-gradle-plugin:$wireVersion"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:$nexusStagingPlugin"
    }
}
apply plugin: 'binary-compatibility-validator'
apply plugin: 'io.codearte.nexus-staging'

apiValidation {
    ignoredProjects += ["sample"]
    ignoredPackages += [
            "com.chuckerteam.chucker.internal",
            "com.chuckerteam.chucker.databinding"
    ]
}

allprojects {
    version = VERSION_NAME
    group = GROUP

    repositories {
        google()
        mavenCentral()
        jcenter {
            content {
                includeModule("org.jetbrains.trove4j", "trove4j")
                includeModule("org.jetbrains.kotlinx", "kotlinx-html-jvm")
            }
        }
    }

    tasks.withType(Test) {
        testLogging {
            events "skipped", "failed", "passed"
        }
    }
}

task installGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}

task clean(type: Delete) {
    dependsOn(installGitHook)
    delete rootProject.buildDir
}

ext {
    minSdkVersion = 21
    targetSdkVersion = 30
    compileSdkVersion = 30
}

nexusStaging {
    username = findProperty("NEXUS_USERNAME")
    password = findProperty("NEXUS_PASSWORD")
    stagingProfileId = "ea09119de9f4"
}
